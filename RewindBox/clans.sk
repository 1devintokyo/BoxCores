options:
	maxPlayersPerClan: 8

on load:
	delete {illegalclannames::*}
	add "fuck" to {illegalclannames::*}
	add "nigger" to {illegalclannames::*}
	add "whore" to {illegalclannames::*}
	add "dick" to {illegalclannames::*}
	add "vagina" to {illegalclannames::*}
	add "porn" to {illegalclannames::*}
	add "sex" to {illegalclannames::*}
	add "suck" to {illegalclannames::*}
	add "niger" to {illegalclannames::*}
	add "niga" to {illegalclannames::*}
	add "nigga" to {illegalclannames::*}
	add "faggot" to {illegalclannames::*}
	add "kkk" to {illegalclannames::*}
	add "blackies" to {illegalclannames::*}
	add "blackass" to {illegalclannames::*}
	add "racist" to {illegalclannames::*}
	add "cracker" to {illegalclannames::*}
	add "beaner" to {illegalclannames::*}
	add "kys" to {illegalclannames::*}
	add "penis" to {illegalclannames::*}

on placeholderapi placeholder request for the prefix "clan":
	if {server::clans::players::%player%} is set:
		set result to " &8[&9&l%{server::clans::players::%player%}%&8]"
	else:
		set result to ""
	
command /clanchat <text>:
	aliases: cchat
	usage: &7Incorrect usage: /clanchat <message>
	trigger:
		if {server::clans::players::%player%} is not set:
			message "&7You're not in a clan!"
			stop
	
		loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
			message "&8[&9&lCLAN CHAT&8] &f%player% &9» &f%colored arg-1%" to loop-value

on chat:
	{server::clans::players::%player%} is set
	{server::clans::players::%player%::chat} is set
	cancel event
	loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
		message "&8[&9&lCLAN CHAT&8] &f%player% &9» &f%colored message%" to loop-value

on death:
	attacker is a player
	victim is a player
	if {server::clans::players::%attacker%} is set:
		add 1 to {server::clans::clans::%{server::clans::players::%attacker%}%::kills}

	if {server::clans::players::%victim%} is set:
		add 1 to {server::clans::clans::%{server::clans::players::%victim%}%::deaths}

command /resetclanlb:
	permission: *
	trigger:
		loop {server::clans::clans::*}:
			set {server::clans::clans::%loop-index%::kills} to 0
			set {server::clans::clans::%loop-index%::deaths} to 0
		message "&aReset clan leaderboards"

command /clan <text> [<text>]:
	aliases: /clans
	usage: &7Incorrect usage: /clan help
	trigger:
		if arg-1 is "create":
			if arg-2 is not set:
				message "&7Incorrect usage: /clan create <name>"
				stop
				
			if arg-2 is set:
				set {_x} to arg-2
				set {_y} to {_x} in lowercase
				loop {illegalclannames::*}:
					if {_y} contains "%loop-value%":
						message "&cYou can't use Vulgarities in your clan name."
						stop
			
			if length of arg-2 > 8:
				message "&7Clan names can only be up to &98 &7characters."
				stop
			
			if arg 2 does not match "[a-zA-Z]+":
				message "&7Name must be &9alphabetical."
				stop
			
			if {server::clans::players::%player%} is set:
				message "&7You're already in a clan!"
				stop
			
			if {server::clans::clans::%arg-2%} is set:
				message "&7A clan with that name already exists!"
				stop
			
			set {server::clans::players::%player%} to arg-2
			
			set {server::clans::clans::%arg-2%} to true
			set {server::clans::clans::%arg-2%::leader} to player

			add player to {server::clans::clans::%arg-2%::members::*}
			
			broadcast "&9&l%player% &7has created a clan named &b%arg-2%&7!"
        
		else if arg-1 is "chat":
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop

			if {server::clans::players::%player%::chat} is set:
				delete {server::clans::players::%player%::chat}
				message "&7Clan chat toggle disabled!"
			else:
				set {server::clans::players::%player%::chat} to true
				message "&7Clan chat toggle enabled!"

		else if arg-1 is "disband":
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop
			
			if {server::clans::clans::%{server::clans::players::%player%}%::leader} is not player:
				message "&7You aren't the leader of your clan!"
				stop
			
			broadcast "&9%player% &7has disbanded the &9%{server::clans::players::%player%}% &7clan!"
			set {_clan} to {server::clans::players::%player%}
			
			loop {server::clans::clans::%{_clan}%::members::*}:
				delete {server::clans::players::%loop-value%}
			
			delete {server::clans::clans::%{_clan}%::*}
			delete {server::clans::clans::%{_clan}%}
		
		else if arg-1 is "transfer":
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop
			set {_clan} to {server::clans::players::%player%}
			
			if {server::clans::clans::%{_clan}%::leader} is not player:
				message "&7You aren't the leader of your clan!"
				stop

			set {_transferring} to arg-2 parsed as an offline player
			if "%{_transferring}%" is "<none>":
				message "&cThere is no player with the name %arg-2%"
				stop
			
			if {server::clans::players::%{_transferring}%} is not {_clan}:
				message "&cThat player isn't in your clan!"
				stop

			set {server::clans::clans::%{_clan}%::leader} to {_transferring}
			broadcast "&9%player% &7has transferred ownership of the &9%{server::clans::players::%player%}% &7clan to &9%{_transferring}%!"
			
		else if arg-1 is "leave":
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop
			
			if {server::clans::clans::%{server::clans::players::%player%}%::leader} is player:
				message "&7You cannot leave your own clan! &cUse /clan disband!"
				stop

			
			loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
				send "&9%player% &7has left the clan!" to loop-value
			
			remove player from {server::clans::clans::%{server::clans::players::%player%}%::members::*}
			delete {server::clans::players::%player%}
	
		else if arg-1 is "invite":
			
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop
			
			if {server::clans::clans::%{server::clans::players::%player%}%::leader} is not player:
				message "&7You're not the owner of the clan!"
				stop
			
			if arg-2 is not set:
				message "&7Incorrect usage: /clan invite <player>"
				stop
			
			set {_p} to arg-2 parsed as player
			
			if {server::clans::clans::%{server::clans::players::%player%}%::members::*} contains {_p}:
				message "&7This player is already in the clan"
			
			
			if {server::clans::clans::%{server::clans::players::%player%}%::invites::*} contains {_p}:
				remove {_p} from {server::clans::clans::%{server::clans::players::%player%}%::invites::*}
				
				loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
					send "&9%player% &7revoked &9%{_p}%&7's invite." to loop-value
			else:
				add {_p} to {server::clans::clans::%{server::clans::players::%player%}%::invites::*}
				
				loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
					send "&9%player% &7invited &9%{_p}% &7to the clan!" to loop-value
				message "&9%player% &7invited you to the &9%{server::clans::players::%player%}% &7clan!" to {_p}
		
		else if arg-1 is "join":
			if arg-2 is not set:
				message "&7Incorrect usage: /clan join <clan name>"
				stop
				
			if {server::clans::players::%player%} is set:
				message "&7You're already in a clan!"
				stop
			
			if {server::clans::clans::%arg-2%::invites::*} does not contain player:
				message "&7You require a invite to join this clan"
				stop
				
			if size of {server::clans::clans::%arg-2%::members::*} >= {@maxPlayersPerClan}:
				message "&7This clan already has the max amount of players"
				stop

			set {server::clans::players::%player%} to {server::clans::players::%{server::clans::clans::%arg-2%::leader}%}
			
			loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
				send "&9%player% &7joined the clan!" to loop-value
			
			remove player from {server::clans::clans::%arg-2%::invites::*}
			add player to {server::clans::clans::%arg-2%::members::*}
			
			message "&7You joined the &9%arg-2% &7clan!"
		
		else if arg-1 is "info":
			set {_clan} to arg-2
			if arg-2 is not set:
				if {server::clans::players::%player%} is set:
					set {_clan} to {server::clans::players::%player%}
				else:
					message "&7Incorrect usage: /clan info <clan name>"
					stop
					
			if {server::clans::clans::%{_clan}%} is not set:
				message "&7This clan does not exist"
				stop
			
			set {_members::*} to {server::clans::clans::%{_clan}%::members::*}
			remove {server::clans::clans::%{_clan}%::leader} from {_members::*}
			
			loop {_members::*}:
				if loop-value is online:
					add "&a%loop-value%&7" to {_display::*}
				else:
					add "&7%loop-value%&7" to {_display::*}
			
			message ""
			message "&9%{_clan}%&9's Information:"
			message ""
			message "&9Leader: &7%{server::clans::clans::%{_clan}%::leader}%"
			if size of {_members::*} is more than 0:
				message "&9Members: &7%{_display::*}%"
			if {server::clans::clans::%{_clan}%::kills} is set:
				message "&9Kills: &7%{server::clans::clans::%{_clan}%::kills}%"
			if {server::clans::clans::%{_clan}%::deaths} is set:
				message "&9Deaths: &7%{server::clans::clans::%{_clan}%::deaths}%"
			message ""
		
		else if arg-1 is "kick":
			if {server::clans::players::%player%} is not set:
				message "&7You're not in a clan!"
				stop

			if {server::clans::clans::%{server::clans::players::%player%}%::leader} is not player:
				message "&7You're not the owner of the clan!"
				stop
			
			if arg-2 is not set:
				message "&7Incorrect usage: /clan kick <player>"
				stop
				
			set {_p} to arg-2 parsed as offlineplayer
			
			if {server::clans::clans::%{server::clans::players::%player%}%::members::*} does not contain {_p}:
				message "&7This player is not in your clan."
				stop
			
			if {_p} is player:
				message "&7You can not kick yourself"
				stop
			
			remove {_p} from {server::clans::clans::%{server::clans::players::%player%}%::members::*}
			delete {server::clans::players::%{_p}%}
			
			loop {server::clans::clans::%{server::clans::players::%player%}%::members::*}:
				message "&9%player% &7kicked &9%{_p}% &7from the clan!" to loop-value
			
			message "&9%player% &7kicked you from the clan!" to {_p}
				
		else:
			message "&9/clan create <name> &7&oCreates a clan."
			message "&9/clan transfer <player> &7&oTransfers ownership of your clan to another player."
			message "&9/clan kick <player> &7&oKicks a player from your clan."
			message "&9/clan invite <player> &7&oInvites a player to your clan"
			message "&9/clan join <name> &7&oAllows you to join clans."
			message "&9/clan leave &7&oAllows you to leave your clan."
			message "&9/clanchat <message> &7&oSends a message to only your clan members."

on tab complete of "/clan":
	set tab completions for position 1 to "create", "kick", "invite", "join", "leave", "transfer", "chat", and "info"

on damage:
	victim is not attacker
	victim is a player
	attacker is a player
	{server::clans::players::%victim%} is set
	{server::clans::players::%victim%} is {server::clans::players::%attacker%}
	cancel event
	message "&9This player is in your clan!" to attacker